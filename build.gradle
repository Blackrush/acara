buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

allprojects {
    version = '1.2.3-SNAPSHOT'
    group = 'com.github.blackrush.acara'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/blackrush/maven' }
    }

    dependencies {
        compile 'com.google.guava:guava:17.0'
        compile 'org.fungsi:fungsi-core:1.0'
        compile 'org.slf4j:slf4j-api:1.7.7'
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testRuntime 'ch.qos.logback:logback-classic:1.1.2'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }

                artifactId = "acara-${project.name}"
            }
        }
    }

    bintray {
        user = hasProperty('bintrayUser') ? bintrayUser : ''
        key = hasProperty('bintrayKey') ? bintrayKey : ''
        publications = ['mavenJava']

        pkg {
            repo = 'maven'
            name = 'acara'
            licenses = ['MIT']
        }
    }

    bintrayUpload.dependsOn publishToMavenLocal
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
